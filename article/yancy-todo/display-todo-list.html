<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <link href="/theme/css/normalize.css" rel="stylesheet">
        <link href="/theme/css/skeleton.css" rel="stylesheet">
        <link href="/theme/css/statocles-default.css" rel="stylesheet">
        <link href="//maxcdn.bootstrapcdn.com/font-awesome/4.3.0/css/font-awesome.min.css" rel="stylesheet">
        <title>Display Todo List - Todo App - Yancy Tutorial - preaction</title>
        <meta content="Statocles 0.094" name="generator">
        
    </head>
    <body>
        <header>
            <nav class="navbar">
                <div class="container">
                    <a class="brand" href="/">preaction</a>
                    <ul>
                        <li>
                            <a href="/index.html">Blog</a>
                        </li>
                        <li>
                            <a href="/resume">Resume</a>
                        </li>
                        <li>
                            <a href="/talks">Talks</a>
                        </li>
                        <li>
                            <a href="/statocles">Statocles</a>
                        </li>
                        <li>
                            <a href="/yertl">Yertl</a>
                        </li>
                        <li>
                            <a href="http://indiepalate.com">Food</a>
                        </li>
                    </ul>
                    
                </div>
            </nav>
            
        </header>
        <div class="main container">
            

<ul class="pager">
    <li class="prev">
        <a class="button" href="populate-data.html">
            &lt; 5. Populate the recurring to-do data
        </a>
    </li>
    <li class="next">
        <a class="button button-primary" href="mark-todo-completed.html">
            7. Mark to-do items as completed &gt;
        </a>
    </li>
</ul>

<main>
    <h1>Display the To-do List</h1>

<p>Now that we have some log items, let&#39;s display them! When a user visits
our root page (<code>/</code>), we want to display the current date and the list of
the log entries that are available to complete for that date. So let&#39;s
edit our <code>get &#39;/&#39;</code> route.</p>

<p>Currently, our route displays our <code>index.html.ep</code> template just by being
named <code>&#39;index&#39;</code>:</p>

<pre><code>get &#39;/&#39; =&gt; &#39;index&#39;;
</code></pre>

<p>Route names in Mojolicious are used to set the default template to
render, and to refer to routes later when we want to build URLs or
redirect to a specific route. The route&#39;s name is different from its URL
so that the URL can change but the route name can stay the same, making
sure that all the links to the route are updated appropriately.</p>

<p>In addition to giving a route a name, we can also give it a subroutine
to handle the request and help generate a response. Building a response
in Mojolicious mostly means adding data to the <code>stash</code> and rendering a
template.</p>

<p>The subroutine must be the second argument to the <code>get</code> function, so our
route will now look like this:</p>

<pre><code>get &#39;/&#39; =&gt; sub {
    my ( $c ) = @_;
    ...;
} =&gt; &#39;index&#39;;
</code></pre>

<h2>Collecting Data in the Stash</h2>

<p>First, we start by finding out what today is, which we already know how
to do:</p>

<pre><code>get &#39;/&#39; =&gt; sub {
    my ( $c ) = @_;
    my $dt = DateTime-&gt;today;
</code></pre>

<p>Next we need to get the items that should appear today. But, before we
can get these items, we should make sure that they exist by calling our
<code>build_todo_log</code> helper.</p>

<pre><code>    $c-&gt;build_todo_log( $dt );
</code></pre>

<p>Now we can get our items. These items should have a start date less than
or equal to today&#39;s date, and an end date greater than or equal to
today&#39;s date. We need to know the ID of the log entry (for later), the
text of the todo item (joining the <code>todo_item</code> table), and when (if) the
log entry was completed.</p>

<pre><code>    my $sql = &lt;&lt;&#39;        SQL&#39;;
        SELECT log.id, item.title, log.complete
        FROM todo_log log
        JOIN todo_item item
            ON log.todo_item_id = item.id
        WHERE log.start_date &lt;= ?::date
            AND log.end_date &gt;= ?::date
    SQL
    my $result = $c-&gt;pg-&gt;db-&gt;query( $sql, ( $dt-&gt;ymd ) x 2 );
    my $items = $result-&gt;hashes;
</code></pre>

<p>Then we can render our template, passing in all the data we have (our
date and our items).</p>

<pre><code>    return $c-&gt;render(
        template =&gt; &#39;index&#39;,
        date =&gt; $dt,
        items =&gt; $items,
    );
</code></pre>

<p>The arguments to the <code>render()</code> method get added to the current
request&#39;s <code>stash</code>. We could also add things explicitly using the
<a href="http://mojolicious.org/perldoc/Mojolicious/Controller#stash">Mojolicious::Controller stash() method</a>.</p>

<p>Our entire route looks like this:</p>

<pre><code>get &#39;/&#39; =&gt; sub {
    my ( $c ) = @_;
    my $dt = DateTime-&gt;today;
    $c-&gt;build_todo_log( $dt );
    my $sql = &lt;&lt;&#39;    SQL&#39;;
        SELECT log.id, item.title, log.complete
        FROM todo_log log
        JOIN todo_item item
            ON log.todo_item_id = item.id
        WHERE log.start_date &lt;= ?::date
            AND log.end_date &gt;= ?::date
    SQL
    my $result = $c-&gt;pg-&gt;db-&gt;query( $sql, ($dt-&gt;ymd) x 2 );
    my $items = $result-&gt;hashes;
    return $c-&gt;render(
        template =&gt; &#39;index&#39;,
        date =&gt; $dt,
        items =&gt; $items,
    );
} =&gt; &#39;index&#39;;
</code></pre>

<h2>Rendering the Template</h2>

<p>Once we&#39;ve prepared all the data our template needs, we need to change
our template to display the data. Down below our <code>__DATA__</code> marker, in
our <code>@@ index.html.ep</code> template, we need to show the current date and
display the list of items we need to do today.</p>

<p>In our template, each stash item is available as a scalar variable in
our template: <code>date</code> in the stash becomes <code>$date</code> in the template, and
the same for <code>items</code>:</p>

<pre><code>@@ index.html.ep
% layout &#39;default&#39;;
% title &#39;Welcome&#39;;
&lt;h1&gt;&lt;%= $date-&gt;ymd %&gt;&lt;/h1&gt;
% for my $item ( @$items ) {
&lt;p&gt;&lt;%= $item-&gt;{title} %&gt;&lt;/p&gt;
% }
</code></pre>

<p>In our new template, we use the <code>ymd()</code> method of the DateTime object to
get a simple date in <code>&lt;year&gt;-&lt;month&gt;-&lt;day&gt;</code> format. Then we loop over
the arrayref of items to display the item&#39;s title.</p>

<p>Now let&#39;s give it a test. When we&#39;re doing rapid development, it&#39;s nice
to not have to restart a server to load new code, so Mojolicious ships
with a command called <code>morbo</code> that automatically restarts the server
when there&#39;s new code. Let&#39;s use <code>morbo</code> to run our server:</p>

<pre><code>$ carton exec morbo myapp.pl
Server available at http://127.0.0.1:3000
</code></pre>

<p>Then we can look at our work so far: ï¿¼</p>

<p><img alt="The list of items to do today" src="display-todo-list.png"></p>

<p>Our full code now looks like this:</p>

<p><button class="btn btn-primary" onclick="document.getElementById( &#39;collapse-153628161531.9826300417827&#39; ).classList.toggle( &#39;in&#39; )">
    Expand code
</button></p>

<div class="collapse" id="collapse-153628161531.9826300417827">
    <pre><code>#!/usr/bin/env perl
use Mojolicious::Lite;
use Mojo::Util qw( unindent trim );
use Mojo::Pg;
use DateTime::Event::Recurrence;
use DateTime;

helper pg =&gt; sub { state $pg = Mojo::Pg-&gt;new( &#39;postgres:///myapp&#39; ) };
app-&gt;pg-&gt;auto_migrate(1)-&gt;migrations-&gt;from_data;

plugin Yancy =&gt; {
    backend =&gt; { Pg =&gt; app-&gt;pg },
    read_schema =&gt; 1,
    collections =&gt; {
        mojo_migrations =&gt; {
            &#39;x-ignore&#39; =&gt; 1,
        },
        todo_item =&gt; {
            title =&gt; &#39;To-Do Item&#39;,
            description =&gt; unindent( trim q{
                A recurring task to do. Tasks are available during a `period`, and
                recur after an `interval`. For example:

                * A task with an interval of `1` and a period of `day` should be
                  completed once every day.
                * A task with an interval of `1` and a period of `week` should be
                  completed once every week.
                * A task with an interval of `2` and a period of `day` will show up
                  once every 2 days and should be completed that day.
                * A task with an interval of `2` and a period of `week` will show up
                  once every 2 weeks and should be completed that week.
            }),
            example =&gt; {
                period =&gt; &quot;day&quot;,
                title =&gt; &quot;Did you brush your teeth?&quot;,
                interval =&gt; 1,
            },
            properties =&gt; {
                period =&gt; {
                    description =&gt; &#39;How long a task is available to do.&#39;,
                },
                interval =&gt; {
                    description =&gt; &#39;The number of periods each between each instance.&#39;,
                },
                start_date =&gt; {
                    description =&gt; &#39;The date to start using this item. Defaults to today.&#39;,
                },
            },
            &#39;x-list-columns&#39; =&gt; [qw( title interval period )],
        },
        todo_log =&gt; {
            title =&gt; &#39;To-Do Log&#39;,
            description =&gt; unindent( trim q{
                A log of the to-do items that have passed. Items can either be completed
                or uncompleted.
            } ),
            properties =&gt; {
                complete =&gt; {
                    description =&gt; &#39;The date which this item was completed, if any.&#39;,
                },
            },
        },
    },
};

my %RECUR_METHOD = (
    day =&gt; &#39;daily&#39;,
    week =&gt; &#39;weekly&#39;,
    month =&gt; &#39;monthly&#39;,
);
sub _build_recurrence {
    my ( $todo_item ) = @_;
    my $method = $RECUR_METHOD{ $todo_item-&gt;{ period } };
    return DateTime::Event::Recurrence-&gt;$method(
        interval =&gt; $todo_item-&gt;{ interval },
    );
}

sub _build_end_dt {
    my ( $todo_item, $start_dt ) = @_;
    if ( $todo_item-&gt;{ period } eq &#39;day&#39; ) {
        return $start_dt-&gt;clone;
    }
    elsif ( $todo_item-&gt;{ period } eq &#39;week&#39; ) {
        return $start_dt-&gt;clone-&gt;add( days =&gt; 6 );
    }
    elsif ( $todo_item-&gt;{ period } eq &#39;month&#39; ) {
        return $start_dt-&gt;clone-&gt;add( months =&gt; 1 )-&gt;subtract( days =&gt; 1 );
    }
}

helper _log_exists =&gt; sub {
    my ( $c, $todo_item, $start_dt ) = @_;
    my $exists_sql = &lt;&lt;&#39;    SQL&#39;;
        SELECT COUNT(*) FROM todo_log
        WHERE todo_item_id = ? AND start_date = ?
    SQL
    my $result = $c-&gt;pg-&gt;db-&gt;query( $exists_sql, $todo_item-&gt;{id}, $start_dt-&gt;ymd );
    my $exists = !!$result-&gt;array-&gt;[0];
    return $exists;
};

helper ensure_log_item_exists =&gt; sub {
    my ( $c, $todo_item, $start_dt ) = @_;
    if ( !$c-&gt;_log_exists( $todo_item, $start_dt ) ) {
        my $end_dt = _build_end_dt( $todo_item, $start_dt );
        my $insert_sql = &lt;&lt;&#39;        SQL&#39;;
            INSERT INTO todo_log ( todo_item_id, start_date, end_date )
            VALUES ( ?, ?, ? )
        SQL
        $c-&gt;pg-&gt;db-&gt;query( $insert_sql,
            $todo_item-&gt;{id}, $start_dt-&gt;ymd, $end_dt-&gt;ymd,
        );
    }
};

helper build_todo_log =&gt; sub {
    my ( $c, $dt ) = @_;
    $dt //= DateTime-&gt;today;
    my $sql = &#39;SELECT * FROM todo_item WHERE start_date &lt;= ?&#39;;
    my $result = $c-&gt;pg-&gt;db-&gt;query( $sql, $dt-&gt;ymd );
    my $todo_items = $result-&gt;hashes;
    for my $todo_item ( @$todo_items ) {
        my $series = _build_recurrence( $todo_item );
        if ( my $start_dt = $series-&gt;current( $dt ) ) {
            $c-&gt;ensure_log_item_exists( $todo_item, $start_dt );
        }
    }
};

get &#39;/&#39; =&gt; sub {
    my ( $c ) = @_;
    my $dt = DateTime-&gt;today;
    $c-&gt;build_todo_log( $dt );
    my $sql = &lt;&lt;&#39;    SQL&#39;;
        SELECT log.id, item.title, log.complete
        FROM todo_log log
        JOIN todo_item item
            ON log.todo_item_id = item.id
        WHERE log.start_date &lt;= ?::date
            AND log.end_date &gt;= ?::date
    SQL
    my $result = $c-&gt;pg-&gt;db-&gt;query( $sql, ( $dt-&gt;ymd ) x 2 );
    my $items = $result-&gt;hashes;
    return $c-&gt;render(
        template =&gt; &#39;index&#39;,
        date =&gt; $dt,
        items =&gt; $items,
    );
} =&gt; &#39;index&#39;;

app-&gt;start;
__DATA__

@@ index.html.ep
% layout &#39;default&#39;;
% title &#39;My Application&#39;;
&lt;h1&gt;&lt;%= $date-&gt;ymd %&gt;&lt;/h1&gt;
% for my $item ( @$items ) {
&lt;p&gt;&lt;%= $item-&gt;{title} %&gt;&lt;/p&gt;
% }

@@ layouts/default.html.ep
&lt;!DOCTYPE html&gt;
&lt;html&gt;
    &lt;head&gt;&lt;title&gt;&lt;%= title %&gt;&lt;/title&gt;&lt;/head&gt;
    &lt;body&gt;
        %= content
    &lt;/body&gt;
&lt;/html&gt;

@@ migrations
-- 1 up
CREATE TYPE todo_interval AS ENUM ( &#39;day&#39;, &#39;week&#39;, &#39;month&#39; );
CREATE TABLE todo_item (
    id SERIAL PRIMARY KEY,
    title TEXT,
    period todo_interval NOT NULL,
    interval INTEGER DEFAULT 1 CHECK ( interval &gt;= 1 ) NOT NULL,
    start_date DATE NOT NULL DEFAULT CURRENT_DATE
);
CREATE TABLE todo_log (
    id SERIAL PRIMARY KEY,
    todo_item_id INTEGER REFERENCES todo_item ( id ) NOT NULL,
    start_date DATE NOT NULL,
    end_date DATE NOT NULL,
    complete DATE DEFAULT NULL
);
-- 1 down
DROP TABLE todo_log;
DROP TABLE todo_item;
DROP TYPE todo_interval;
</code></pre>

</div>

</main>

<ul class="pager">
    <li class="prev">
        <a class="button" href="populate-data.html">
            &lt; 5. Populate the recurring to-do data
        </a>
    </li>
    <li class="next">
        <a class="button button-primary" href="mark-todo-completed.html">
            7. Mark to-do items as completed &gt;
        </a>
    </li>
</ul>


        </div>
        <footer>
            
            <div class="container tagline">
                <a href="http://preaction.me/statocles">Made with Statocles</a><br>
                <a href="http://www.perl.org">Powered by Perl</a>
            </div>
        </footer>
    </body>
</html>
