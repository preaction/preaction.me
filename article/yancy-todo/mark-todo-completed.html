<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <link href="/theme/css/normalize.css" rel="stylesheet">
        <link href="/theme/css/skeleton.css" rel="stylesheet">
        <link href="/theme/css/statocles-default.css" rel="stylesheet">
        <link href="//maxcdn.bootstrapcdn.com/font-awesome/4.3.0/css/font-awesome.min.css" rel="stylesheet">
        <title>Mark Todo Completed - Todo App - Yancy Tutorial - preaction</title>
        <meta content="Statocles 0.094" name="generator">
        
    </head>
    <body>
        <header>
            <nav class="navbar">
                <div class="container">
                    <a class="brand" href="/">preaction</a>
                    <ul>
                        <li>
                            <a href="/index.html">Blog</a>
                        </li>
                        <li>
                            <a href="/resume">Resume</a>
                        </li>
                        <li>
                            <a href="/talks">Talks</a>
                        </li>
                        <li>
                            <a href="/statocles">Statocles</a>
                        </li>
                        <li>
                            <a href="/yertl">Yertl</a>
                        </li>
                        <li>
                            <a href="http://indiepalate.com">Food</a>
                        </li>
                    </ul>
                    
                </div>
            </nav>
            
        </header>
        <div class="main container">
            

<ul class="pager">
    <li class="prev">
        <a class="button" href="display-todo-list.html">
            &lt; 6. Display the to-do list
        </a>
    </li>
    <li class="next">
        <a class="button button-primary" href="navigate-todo-list.html">
            8. Navigate between dates &gt;
        </a>
    </li>
</ul>

<main>
    <h1>Mark To-do Items as Completed</h1>

<p>Once we&#39;ve displayed our to-do list, we need to be able to mark our
to-do items as completed. We will turn each of our todo log entries into
a form with a button. Clicking on the button will mark the item as
completed, or, if it&#39;s already completed, will mark it as incomplete (in
case we mistakenly complete something we haven&#39;t done).</p>

<h2>Building the Update Route Handler</h2>

<p>First, we should build a route to mark an entry as completed. This will
be a <code>POST</code> request, so we use the <code>post</code> function. We&#39;re going to put
the ID of the log entry in the URL to make it REST-like.</p>

<pre><code>post &#39;/log/:log_id&#39; =&gt; sub {
    my ( $c ) = @_;
</code></pre>

<p>The <code>:</code> in the route path introduces a placeholder. The text following
the <code>:</code> is where Mojolicious will save the value in the <code>stash</code>, which
is a general storage area for the current request. So, we can get the ID
out using the <code>stash()</code> method of the controller object.</p>

<pre><code>    my $id = $c-&gt;stash( &#39;log_id&#39; );
</code></pre>

<p>Our form will have a button that will have a value of true (<code>1</code>) or
false (<code>0</code>) if we are marking the item as complete or incomplete. Form
values are retrieved by using the <code>param()</code> method of the controller
object. If the item is complete, we want to store the date that the item
was completed. Otherwise we want to set the completed date to <code>null</code>
(from the Perl side, we use <code>undef</code>).</p>

<pre><code>    my $complete = $c-&gt;param( &#39;complete&#39; )
        ? DateTime-&gt;today-&gt;ymd
        : undef;
</code></pre>

<p>Then we can execute a SQL <code>UPDATE</code> query to update our log entry.</p>

<pre><code>    my $sql = &#39;UPDATE todo_log SET complete = ? WHERE id = ?&#39;;
    $c-&gt;pg-&gt;db-&gt;query( $sql, $complete, $id );
</code></pre>

<p>Finally, we redirect the user back to where they were, our <code>index</code> page,
with the <code>redirect_to()</code> method, which takes the name of the route to
redirect to as an argument.</p>

<pre><code>    return $c-&gt;redirect_to( &#39;index&#39; );
</code></pre>

<p>This route needs a name, so we&#39;ll give it a name of <code>update_log</code>. We&#39;re
going to use this name to build a form that gets submitted to the right
URL.</p>

<h2>Creating the Button Form</h2>

<p>Down in our template, rather than displaying an unordered list, now
we&#39;re going to render a form for every item in the list.</p>

<pre><code>@@ index.html.ep
% layout &#39;default&#39;;
% title &#39;Welcome&#39;;
&lt;h1&gt;&lt;%= $date-&gt;ymd %&gt;&lt;/h1&gt;
% for my $item ( @$items ) {
    %= form_for &#39;update_log&#39;, { log_id =&gt; $item-&gt;{id} }, begin
        %= $item-&gt;{title}
        % if ( !$item-&gt;{complete} ) {
            &lt;button name=&quot;complete&quot; value=&quot;1&quot;&gt;Complete&lt;/button&gt;
        % }
        % else {
            &lt;button name=&quot;complete&quot; value=&quot;0&quot;&gt;Undo&lt;/button&gt;
        % }
    % end
% }
</code></pre>

<p>This uses the <code>form_for</code> helper. Helper functions are also available in
every template (making them truly helpful). The <code>form_for</code> helper is
part of a set of helpers installed by default from the
<a href="http://mojolicious.org/perldoc/Mojolicious/Plugin/TagHelpers">Mojolicious::Plugin::TagHelpers</a>
module. It takes the name of a route, a hash reference of placeholder
values (in this case, we need to give it the ID of the log entry), and
then we use the <code>begin</code> function.</p>

<p>The <code>begin</code> function allows us to add a template as an argument to
a function. The <code>end</code> function marks the end of the content, and then
the whole thing is given to the original function (in this case,
<code>form_for</code>). In this way, the <code>form_for</code> function will wrap our content
in <code>&lt;form&gt;</code> HTML tags.</p>

<p>Inside our form we print out the text of the todo item.</p>

<p>Finally, we add the button. If the item is not completed, we&#39;ll make
a button that marks the item as complete. If the item is completed,
we&#39;ll make a button that marks the item as incomplete.</p>

<p>Now we can see our button and click on it to complete our to-do list!</p>

<p><img alt="The todo list with complete/undo buttons" src="mark-todo-completed.png"></p>

<p>Our full code looks like this:</p>

<p><button class="btn btn-primary" onclick="document.getElementById( &#39;collapse-152288556676.3886346410782&#39; ).classList.toggle( &#39;in&#39; )">
    Expand code
</button></p>

<div class="collapse" id="collapse-152288556676.3886346410782">
    <pre><code>#!/usr/bin/env perl
use Mojolicious::Lite;
use Mojo::Util qw( unindent trim );
use Mojo::Pg;
use DateTime::Event::Recurrence;
use DateTime;

helper pg =&gt; sub { state $pg = Mojo::Pg-&gt;new( &#39;postgres:///myapp&#39; ) };
app-&gt;pg-&gt;auto_migrate(1)-&gt;migrations-&gt;from_data;

plugin Yancy =&gt; {
    backend =&gt; { Pg =&gt; app-&gt;pg },
    read_schema =&gt; 1,
    collections =&gt; {
        mojo_migrations =&gt; {
            &#39;x-ignore&#39; =&gt; 1,
        },
        todo_item =&gt; {
            title =&gt; &#39;To-Do Item&#39;,
            description =&gt; unindent( trim q{
                A recurring task to do. Tasks are available during a `period`, and
                recur after an `interval`. For example:

                * A task with an interval of `1` and a period of `day` should be
                  completed once every day.
                * A task with an interval of `1` and a period of `week` should be
                  completed once every week.
                * A task with an interval of `2` and a period of `day` will show up
                  once every 2 days and should be completed that day.
                * A task with an interval of `2` and a period of `week` will show up
                  once every 2 weeks and should be completed that week.
            }),
            example =&gt; {
                period =&gt; &quot;day&quot;,
                title =&gt; &quot;Did you brush your teeth?&quot;,
                interval =&gt; 1,
            },
            properties =&gt; {
                period =&gt; {
                    description =&gt; &#39;How long a task is available to do.&#39;,
                },
                interval =&gt; {
                    description =&gt; &#39;The number of periods each between each instance.&#39;,
                },
                start_date =&gt; {
                    description =&gt; &#39;The date to start using this item. Defaults to today.&#39;,
                },
            },
            &#39;x-list-columns&#39; =&gt; [qw( title interval period )],
        },
        todo_log =&gt; {
            title =&gt; &#39;To-Do Log&#39;,
            description =&gt; unindent( trim q{
                A log of the to-do items that have passed. Items can either be completed
                or uncompleted.
            } ),
            properties =&gt; {
                complete =&gt; {
                    description =&gt; &#39;The date which this item was completed, if any.&#39;,
                },
            },
        },
    },
};

my %RECUR_METHOD = (
    day =&gt; &#39;daily&#39;,
    week =&gt; &#39;weekly&#39;,
    month =&gt; &#39;monthly&#39;,
);
sub _build_recurrence {
    my ( $todo_item ) = @_;
    my $method = $RECUR_METHOD{ $todo_item-&gt;{ period } };
    return DateTime::Event::Recurrence-&gt;$method(
        interval =&gt; $todo_item-&gt;{ interval },
    );
}

sub _build_end_dt {
    my ( $todo_item, $start_dt ) = @_;
    if ( $todo_item-&gt;{ period } eq &#39;day&#39; ) {
        return $start_dt-&gt;clone;
    }
    elsif ( $todo_item-&gt;{ period } eq &#39;week&#39; ) {
        return $start_dt-&gt;clone-&gt;add( days =&gt; 6 );
    }
    elsif ( $todo_item-&gt;{ period } eq &#39;month&#39; ) {
        return $start_dt-&gt;clone-&gt;add( months =&gt; 1 )-&gt;subtract( days =&gt; 1 );
    }
}

helper _log_exists =&gt; sub {
    my ( $c, $todo_item, $start_dt ) = @_;
    my $exists_sql = &lt;&lt;&#39;    SQL&#39;;
        SELECT COUNT(*) FROM todo_log
        WHERE todo_item_id = ? AND start_date = ?
    SQL
    my $result = $c-&gt;pg-&gt;db-&gt;query( $exists_sql, $todo_item-&gt;{id}, $start_dt-&gt;ymd );
    my $exists = !!$result-&gt;array-&gt;[0];
    return $exists;
};

helper ensure_log_item_exists =&gt; sub {
    my ( $c, $todo_item, $start_dt ) = @_;
    if ( !$c-&gt;_log_exists( $todo_item, $start_dt ) ) {
        my $end_dt = _build_end_dt( $todo_item, $start_dt );
        my $insert_sql = &lt;&lt;&#39;        SQL&#39;;
            INSERT INTO todo_log ( todo_item_id, start_date, end_date )
            VALUES ( ?, ?, ? )
        SQL
        $c-&gt;pg-&gt;db-&gt;query( $insert_sql,
            $todo_item-&gt;{id}, $start_dt-&gt;ymd, $end_dt-&gt;ymd,
        );
    }
};

helper build_todo_log =&gt; sub {
    my ( $c, $dt ) = @_;
    $dt //= DateTime-&gt;today;
    my $sql = &#39;SELECT * FROM todo_item WHERE start_date &lt;= ?&#39;;
    my $result = $c-&gt;pg-&gt;db-&gt;query( $sql, $dt-&gt;ymd );
    my $todo_items = $result-&gt;hashes;
    for my $todo_item ( @$todo_items ) {
        my $series = _build_recurrence( $todo_item );
        if ( my $start_dt = $series-&gt;current( $dt ) ) {
            $c-&gt;ensure_log_item_exists( $todo_item, $start_dt );
        }
    }
};

get &#39;/&#39; =&gt; sub {
    my ( $c ) = @_;
    my $dt = DateTime-&gt;today;
    $c-&gt;build_todo_log( $dt );
    my $sql = &lt;&lt;&#39;    SQL&#39;;
        SELECT log.id, item.title, log.complete
        FROM todo_log log
        JOIN todo_item item
            ON log.todo_item_id = item.id
        WHERE log.start_date &lt;= ?::date
            AND log.end_date &gt;= ?::date
    SQL
    my $result = $c-&gt;pg-&gt;db-&gt;query( $sql, ( $dt-&gt;ymd ) x 2 );
    my $items = $result-&gt;hashes;
    return $c-&gt;render(
        template =&gt; &#39;index&#39;,
        date =&gt; $dt,
        items =&gt; $items,
    );
} =&gt; &#39;index&#39;;

post &#39;/log/:log_id&#39; =&gt; sub {
    my ( $c ) = @_;
    my $id = $c-&gt;stash( &#39;log_id&#39; );
    my $complete = $c-&gt;param( &#39;complete&#39; )
        ? DateTime-&gt;today-&gt;ymd
        : undef;
    my $sql = &#39;UPDATE todo_log SET complete = ? WHERE id = ?&#39;;
    $c-&gt;pg-&gt;db-&gt;query( $sql, $complete, $id );
    return $c-&gt;redirect_to( &#39;index&#39; );
} =&gt; &#39;update_log&#39;;

app-&gt;start;
__DATA__

@@ index.html.ep
% layout &#39;default&#39;;
% title &#39;Welcome&#39;;
&lt;h1&gt;&lt;%= $date-&gt;ymd %&gt;&lt;/h1&gt;
% for my $item ( @$items ) {
    %= form_for &#39;update_log&#39;, { log_id =&gt; $item-&gt;{id} }, begin
        %= $item-&gt;{title}
        % if ( !$item-&gt;{complete} ) {
            &lt;button name=&quot;complete&quot; value=&quot;1&quot;&gt;Complete&lt;/button&gt;
        % }
        % else {
            &lt;button name=&quot;complete&quot; value=&quot;0&quot;&gt;Undo&lt;/button&gt;
        % }
    % end
% }

@@ layouts/default.html.ep
&lt;!DOCTYPE html&gt;
&lt;html&gt;
    &lt;head&gt;&lt;title&gt;&lt;%= title %&gt;&lt;/title&gt;&lt;/head&gt;
    &lt;body&gt;
        %= content
    &lt;/body&gt;
&lt;/html&gt;

@@ migrations
-- 1 up
CREATE TYPE todo_interval AS ENUM ( &#39;day&#39;, &#39;week&#39;, &#39;month&#39; );
CREATE TABLE todo_item (
    id SERIAL PRIMARY KEY,
    title TEXT,
    period todo_interval NOT NULL,
    interval INTEGER DEFAULT 1 CHECK ( interval &gt;= 1 ) NOT NULL,
    start_date DATE NOT NULL DEFAULT CURRENT_DATE
);
CREATE TABLE todo_log (
    id SERIAL PRIMARY KEY,
    todo_item_id INTEGER REFERENCES todo_item ( id ) NOT NULL,
    start_date DATE NOT NULL,
    end_date DATE NOT NULL,
    complete DATE DEFAULT NULL
);
-- 1 down
DROP TABLE todo_log;
DROP TABLE todo_item;
DROP TYPE todo_interval;
</code></pre>

</div>

</main>

<ul class="pager">
    <li class="prev">
        <a class="button" href="display-todo-list.html">
            &lt; 6. Display the to-do list
        </a>
    </li>
    <li class="next">
        <a class="button button-primary" href="navigate-todo-list.html">
            8. Navigate between dates &gt;
        </a>
    </li>
</ul>


        </div>
        <footer>
            
            <div class="container tagline">
                <a href="http://preaction.me/statocles">Made with Statocles</a><br>
                <a href="http://www.perl.org">Powered by Perl</a>
            </div>
        </footer>
    </body>
</html>
